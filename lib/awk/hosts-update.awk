BEGIN {
    blank = "^[[:blank:]]*$"
    if (!FIRST)
        FIRST = "^# Generated by "
    if (!LAST && !BREAK)
        BREAK = blank
}

function is_blank() {
    return $0 ~ blank
}

function print_hosts() {
    if (!hosts_printed) {
        if (last_blank < last_printed)
            print ""
        print HOSTS
    }
    hosts_printed = 1
    hosts_just_printed = 1
    skip = 0
}

!skip && $0 ~ FIRST {
    skip = 1
    next
}

skip && ((MAX_LINES && skip == MAX_LINES) || (LAST && $0 ~ LAST)) {
    print_hosts()
    next
}

skip && BREAK && $0 ~ BREAK {
    print_hosts()
}

skip {
    skip++
    next
}

is_blank() {
    last_blank = NR
}

{
    if (hosts_just_printed && !is_blank())
        print ""
    print
    hosts_just_printed = 0
    last_printed = NR
}

END {
    HOSTS = HOSTS "\n"
    print_hosts()
}
